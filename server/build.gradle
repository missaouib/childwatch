buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}


plugins {
	id 'io.franzbecker.gradle-lombok' version '1.8'
}

lombok { // optional: values below are the defaults
	version = "1.16.18"
	sha256 = ""
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.franzbecker.gradle-lombok'

version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.flywaydb:flyway-core')
    compile('com.graphql-java:graphql-java-tools:3.1.3');
    compile('com.graphql-java:graphql-java:3.0.0');
    compile('com.graphql-java:graphql-java-servlet:4.0.0');
    compile('com.github.javafaker:javafaker:0.13')
    compile('org.hibernate:hibernate-java8')
    runtime('org.postgresql:postgresql')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

archivesBaseName = 'cw2'

jar {
    from('../client/dist') {
        into('static')
    }
}

bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

springBoot {
    // "Executable" means it runs as ./cw2-1.0.jar, also required for systemd integration
    executable = true
}

task zip(type: Zip, dependsOn: bootRepackage) {
    from('build/libs') {
        include '*.jar'
    }
    from('.') {
        include 'config/SAMPLE*'
        include 'ops/*'
    }
}

zip.dependsOn(build)
