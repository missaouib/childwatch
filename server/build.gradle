buildscript {
    ext {
        springBootVersion = '1.5.7.RELEASE'
        gradleDockerVersion   = '1.2'
    }
    repositories {
        mavenCentral()
        jcenter()
        maven 
        {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("se.transmode.gradle:gradle-docker:${gradleDockerVersion}")
    }
}



apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

version = '2.0.0-BETA.17'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.flywaydb:flyway-core')
    compile('org.hibernate:hibernate-java8')
    runtime('org.postgresql:postgresql')
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

archivesBaseName = 'cw2'


bootRun {
    // support passing -Dsystem.property=value to bootRun task
    systemProperties = System.properties
}

springBoot {
    // "Executable" means it runs as ./cw2-1.0.jar, also required for systemd integration
    executable = true
}

task buildDocker(type: Docker) {
  baseImage = 'develar/java:latest'
  push = project.hasProperty('push')
  tag = 'bisonburger/childwatch'
  addFile {
    from jar
    rename {'app.jar'}
  }
  entryPoint(['java', '-Djava.security.egd=file:/dev/./urandom', '-Dspring.profiles.active=prod', '-jar', '/app.jar'])
  // exposePort(8080)
}

buildDocker.dependsOn(assemble)

